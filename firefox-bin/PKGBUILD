# Maintainer: Alad Wenter <alad@archlinux.org>
# Contributor: Luna Jernberg <droidbittin@gmail.com>
# Contributor: Hilton Medeiros <medeiros.hilton@gmail.com>
# Contributor: Simon Brulhart <simon@brulhart.me>
# Contributor: Det <nimetonmaili g-mail>, Achilleas Pipinellis, speed145a, Schnouki, aus

pkgname=firefox-bin
_pkgname=firefox
pkgver=132.0.1
pkgrel=1
pkgdesc="Standalone web browser from mozilla.org - Static binaries from upstream"
arch=('x86_64' 'i686')
url="https://www.mozilla.org/en-US/firefox/new/"
license=(MPL GPL LGPL)
depends=(gtk3 libxt mime-types dbus-glib nss ttf-font)
optdepends=('ffmpeg: H264/AAC/MP3 decoding'
            'networkmanager: Location detection via available WiFi networks'
            'libnotify: Notification integration'
            'pulseaudio: Audio support'
            'speech-dispatcher: Text-to-Speech'
            'hunspell-en_US: Spell checking, American English')
options=(!strip)
provides=("firefox=$pkgver")
conflicts=('firefox')
_archive="https://archive.mozilla.org/pub/firefox/releases"
source_x86_64=("firefox-$pkgver-x86_64.tar.bz2::$_archive/$pkgver/linux-x86_64/en-US/firefox-$pkgver.tar.bz2"
               "firefox-$pkgver-x86_64.tar.bz2.asc::$_archive/$pkgver/linux-x86_64/en-US/firefox-$pkgver.tar.bz2.asc")
source_i686=("firefox-$pkgver-i686.tar.bz2::$_archive/$pkgver/linux-i686/en-US/firefox-$pkgver.tar.bz2"
             "firefox-$pkgver-i686.tar.bz2.asc::$_archive/$pkgver/linux-i686/en-US/firefox-$pkgver.tar.bz2.asc")
source=($_pkgname.sh
        $_pkgname.desktop
        policies.json)
sha256sums=('9491f195066aa56f4f0bbc83e5f0581a460f4c34ee455ba24db312b540322703'
            'c64008cbf01956ce3dba693230670de58fa9487ad19f4ba76614af13beb1b8ef'
            '1c432537a46ffe64b3069bc2c2e7e069797223607b181e77492e74b38e90c730')
sha256sums_x86_64=('ecdcb4787263cacd31aa3a1b62c14f1d3b69af44a0b40f9eb040852f401097c1'
                   'SKIP')
sha256sums_i686=('f110d84bd70501264612a229210a6e52f1279e7349a32665200f60be685aaf27'
                 'SKIP')
validpgpkeys=('14F26682D0916CDD81E37B6D61B7B526D98F0353') # Mozilla Software Releases <release@mozilla.com>

package() {
  # Create directories
  mkdir -p "$pkgdir"/usr/bin
  mkdir -p "$pkgdir"/usr/share/applications
  mkdir -p "$pkgdir"/opt

  # Install
  cp -r firefox/ "$pkgdir"/opt/$pkgname

  # Launchers
  install -m755 $_pkgname.sh "$pkgdir"/usr/bin/$_pkgname

  # Desktops
  install -m644 *.desktop "$pkgdir"/usr/share/applications/

  # Icons
  for i in 16x16 32x32 48x48 64x64 128x128; do
    install -d "$pkgdir"/usr/share/icons/hicolor/$i/apps/
    ln -s /opt/$pkgname/browser/chrome/icons/default/default${i/x*}.png \
          "$pkgdir"/usr/share/icons/hicolor/$i/apps/$_pkgname.png
  done

  # Use system-provided dictionaries
  #rm -r "$pkgdir"/opt/$_pkgname/dictionaries
  ln -Ts /usr/share/hunspell "$pkgdir"/opt/$pkgname/dictionaries
  ln -Ts /usr/share/hyphen "$pkgdir"/opt/$pkgname/hyphenation

  # Use system certificates
  ln -sf /usr/lib/libnssckbi.so "$pkgdir"/opt/$pkgname/libnssckbi.so

  # Disable update checks (managed by pacman)
  mkdir "$pkgdir"/opt/$pkgname/distribution
  install -m644 "$srcdir"/policies.json "$pkgdir"/opt/$pkgname/distribution/
}

