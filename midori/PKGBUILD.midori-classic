# Maintainer:
# Contributor Alexander F. Rødseth <xyproto@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>
# Contributor: rabyte <rabyte@gmail.com>
# Contributor: Johannes Krampf <wuischke@amule.org>
# Contributor: Bartłomiej Piotrowski <barthalion@gmail.com>

#: ${_commit=f6b3b1e030db7eb2de7faf73ec4ccc18949b668d} # 9.0
: ${_commit=d8546ca689af185a456ec1f394bb89926d7c142a} # 9.0r7

_pkgname="midori-classic"
pkgname="$_pkgname"
pkgver=9.0
pkgrel=1
pkgdesc='Classic lightweight web browser, based on WebKit'
url='https://github.com/midori-browser/core'
license=('LGPL-2.1-or-later')
arch=('x86_64')

depends=(
  'gcr'
  'libarchive'
  'libpeas'
  'webkit2gtk'
)
makedepends=(
  'cmake'
  'git'
  'gobject-introspection'
  'intltool'
  'ninja'
  'vala'
)
optdepends=(
  'gst-plugins-base: HTML5 OGG videos support'
  'gst-plugins-good: HTML5 H264 and WebM videos support'
  'gst-libav: HTML5 H264 videos support'
)

provides=("midori=$pkgver")
conflicts=("midori")

options=('!debug' '!emptydirs')

_pkgsrc="$_pkgname"
source=("$_pkgsrc"::"git+$url.git#commit=$_commit")
sha256sums=('SKIP')

build() {
  export CFLAGS
  CFLAGS+=' -fPIC -Wno-error=int-conversion'

  local _cmake_options=(
    -B build
    -S "$_pkgsrc"
    -G Ninja
    -DCMAKE_BUILD_TYPE=None
    -DCMAKE_INSTALL_PREFIX='/usr'
    -Wno-dev
  )

  cmake "${_cmake_options[@]}"
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
