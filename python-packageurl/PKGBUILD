# Maintainer: Claudia Pellegrino <aur Ã¤t cpellegrino.de>

pkgname=python-packageurl
_gitpkgname=packageurl-python
pkgver=0.16.0
pkgrel=1
pkgdesc='A purl aka. Package URL parser and builder'
arch=('any')
url='https://github.com/package-url/packageurl-python'
license=('MIT')
depends=('python')
makedepends=(
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-wheel'
)
checkdepends=('python-pytest')
optdepends=(
  'python-django-filter: for included contrib scripts'
  'python-sqlalchemy: for included contrib scripts'
)
conflicts=('python-packageurl-git')
options=('!debug' '!strip')
source=("${_gitpkgname}-${pkgver}.tar.gz::https://github.com/package-url/packageurl-python/archive/v${pkgver}.tar.gz")
sha512sums=('ac2ed22f080b3bdf13996438a749b646831de4802a1c4bfe96ab2ac4977ea307daee5f38f6a12d7359824ac37f32cf5c3e1d1c67b2a9b756035e72a40845c356')

build() {
  cd "${srcdir}/${_gitpkgname}-${pkgver}"
  python -m build --wheel --no-isolation
}

check() {
  cd "${srcdir}/${_gitpkgname}-${pkgver}"
  python -m pytest
}

package() {
  cd "${srcdir}/${_gitpkgname}-${pkgver}"
  python -I -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 mit.LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
